import VersoManual
import ConceptualMathematics.Meta.Lean
import ConceptualMathematics.Article2
import Mathlib

open Verso.Genre Manual InlineLean
open ConceptualMathematics
open CategoryTheory


#doc (Manual) "Quiz" =>

%%%
htmlSplit := .never
number := false
%%%

```savedImport
import ConceptualMathematics.Article2
import Mathlib
open CategoryTheory
```

```savedLean -show
namespace CM
local notation:80 g " âŠš " f:80 => CategoryStruct.comp f g
```

:::question (questionTitle := "Problem 1") (questionPage := "108")
Give an example of two explicit sets $`A` and $`B` and an explicit map $`{A \xrightarrow{f} B}` satisfying _both_:

(a) there is a retraction for $`f`, _and_

(b) there is _no_ section for $`f`.

Then explain how you know that $`f` satisfies (a) and (b).
:::

:::solution (solutionTo := "Problem 1")
```savedComment
Problem Quiz.1 (p. 108)
```

We use `Fintype`s here instead of sets.

```savedLean -show
namespace Quiz_1
```

We give explicit types $`A` and $`B` and an explicit map $`f`.

```savedLean
inductive A
  | a
  deriving Fintype

inductive B
  | bâ‚ | bâ‚‚
  deriving Fintype

def f : A âŸ¶ B
  | A.a => B.bâ‚
```

Our candidate retraction for $`f` is

```savedLean
def r : B âŸ¶ A
  | B.bâ‚ => A.a
  | B.bâ‚‚ => A.a
```

and we show that (a) is satisfied.

```savedLean
example : IsRetraction f := by
  use r
  change r âŠš f = ðŸ™ A
  funext x
  fin_cases x
  dsimp [f, r]
```

We show that $`f` also satisfies (b).

```savedLean
example : Â¬(IsSection f) := by
  by_contra h
  obtain âŸ¨s, hsâŸ© := h
  have h_false := congrFun hs B.bâ‚‚
  cases h_false
```

```savedLean -show
end Quiz_1
```
:::

:::question (questionTitle := "Problem 2") (questionPage := "108")
If $`{C \xrightarrow{p} D \xrightarrow{q} C}` satisfy $`{p \circ q \circ p = p}`, can you conclude that

(a) $`{p \circ q}` is idempotent? If so, how?

(b) $`{q \circ p}` is idempotent? If so, how?
:::

:::solution (solutionTo := "Problem 2")
```savedComment
Problem Quiz.2 (p. 108)
```

```savedLean -show
namespace Quiz_2
```

```savedLean
variable {ð’ž : Type u} [Category.{v, u} ð’ž] {C D : ð’ž}
         (p : C âŸ¶ D) (q : D âŸ¶ C) (hpq : p âŠš q âŠš p = p)
```

We show that $`{p \circ q}` is idempotent.

```savedLean
example : IsIdempotent (p âŠš q) := {
  idem := by
    calc (p âŠš q) âŠš p âŠš q
      _ = ((p âŠš q) âŠš p) âŠš q := by rw [Category.assoc]
      _ = (p âŠš q âŠš p) âŠš q := by rw [â† Category.assoc p]
      _ = p âŠš q := by rw [hpq]
}
```

We show that $`{q \circ p}` is idempotent.

```savedLean
example : IsIdempotent (q âŠš p) := {
  idem := by
    calc (q âŠš p) âŠš q âŠš p
      _ = q âŠš p âŠš q âŠš p := by rw [Category.assoc (q âŠš p)]
      _ = q âŠš p := by rw [hpq]
}
```

```savedLean -show
end Quiz_2
```
:::

# Optional questions

:::question (questionTitle := "Problem 2*") (questionPage := "108")
If $`{C \xrightarrow{p} D \xrightarrow{q} C}` satisfy $`{p \circ q \circ p = p}`, use the given maps $`p` and $`q` to devise a map $`q'` satisfying _both_:
$$`p \circ q' \circ p = p`
_and_
$$`q' \circ p \circ q' = q'`
(and explain how you know that your $`q'` has these properties).
:::

:::solution (solutionTo := "Problem 2*")
```savedComment
Problem Quiz_2* (p. 108)
```

```savedLean -show
namespace Â«Quiz_2*Â»
```

```savedLean
variable {ð’ž : Type u} [Category.{v, u} ð’ž] {C D : ð’ž}
         (p : C âŸ¶ D) (q : D âŸ¶ C) (hpq : p âŠš q âŠš p = p)
```

We show that $`{q' = q \circ p \circ q}` has the required properties.

```savedLean
example : âˆƒ q', p âŠš q' âŠš p = p âˆ§ q' âŠš p âŠš q' = q' := by
  use q âŠš p âŠš q -- q'
  constructor
  Â· rw [â† Category.assoc p, â† Category.assoc, hpq, hpq]
  Â· rw [Category.assoc (q âŠš p âŠš q)]
    repeat rw [â† Category.assoc p]
    rw [hpq]
    repeat rw [Category.assoc q]
    rw [â† Category.assoc (q âŠš p), hpq]
```

```savedLean -show
end Â«Quiz_2*Â»
```
:::

:::question (questionTitle := "Problem 1*") (questionPage := "108")
Same question as Problem 1 at top of page, except that both sets $`A` and $`B` are required to be _infinite_ sets.
:::

:::solution (solutionTo := "Problem 1*")
```savedComment
Problem Quiz_1* (p. 108)
```

We use (non-finite) types here instead of infinite sets.

```savedLean -show
namespace Â«Quiz_1*Â»
```

We give explicit types $`A` and $`B` and an explicit map $`f`.

```savedLean
abbrev A := â„•
abbrev B := â„

def f : A âŸ¶ B
  | 0 => 0
  | n + 1 => n + 1
```

Our candidate retraction for $`f` is

```savedLean
noncomputable def r : B âŸ¶ A
  | r => âŒŠabs râŒ‹â‚Š
```

and we show that (a) is satisfied.

```savedLean
example : IsRetraction f := by
  use r
  change r âŠš f = ðŸ™ A
  funext x
  dsimp [f, r]
  induction x with
  | zero => rw [abs_zero, Nat.floor_zero]
  | succ n =>
      dsimp
      norm_cast
      rw [Nat.floor_natCast]
```

We show that $`f` also satisfies (b).

```savedLean
example : Â¬(IsSection f) := by
  by_contra h
  obtain âŸ¨s, hsâŸ© := h
  have h_false := congrFun hs 0.5
  cases hx : s 0.5 with
  | zero =>
      rw [types_comp_apply, hx] at h_false
      dsimp [f] at h_false
      linarith
  | succ n =>
      rw [types_comp_apply, hx] at h_false
      dsimp [f] at h_false
      have h_ge_1 : (1 : B) â‰¤ n + 1 := by linarith
      linarith
```

```savedLean -show
end Â«Quiz_1*Â»
```
:::

```savedLean -show
end CM
```
