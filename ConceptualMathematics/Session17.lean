import VersoManual
import ConceptualMathematics.Meta.Lean
import Mathlib

open Verso.Genre Manual InlineLean
open ConceptualMathematics
open CategoryTheory

set_option maxHeartbeats 400000 -- required for Exercise 17.4(c)


#doc (Manual) "Session 17: Some uses of graphs" =>

%%%
htmlSplit := .never
number := false
%%%

```savedImport
import Mathlib
open CategoryTheory
```

```savedLean -show
namespace CM
local notation:80 g " ‚äö " f:80 => CategoryStruct.comp f g
```

# 1. Paths

In the exercises that follow for this Session, we implement the free category ùë≠$`(G)` on each graph $`G` using mathlib's `CategoryTheory.Paths V`, where `V` is a quiver (i.e., a directed graph). In particular, we make extensive use of the inductive datatype `Quiver.Path`, the type of paths through the arrows of the quiver, which we print below for reference.

```lean (name := out_Quiver_Path)
#print Quiver.Path
```

```leanOutput out_Quiver_Path
inductive Quiver.Path.{v, u} : {V : Type u} ‚Üí [Quiver V] ‚Üí V ‚Üí V ‚Üí Sort (max (u + 1) v)
number of parameters: 3
constructors:
Quiver.Path.nil : {V : Type u} ‚Üí [inst : Quiver V] ‚Üí {a : V} ‚Üí Quiver.Path a a
Quiver.Path.cons : {V : Type u} ‚Üí [inst : Quiver V] ‚Üí {a b c : V} ‚Üí Quiver.Path a b ‚Üí (b ‚ü∂ c) ‚Üí Quiver.Path a c
```

:::question (questionTitle := "Exercise 1") (questionPage := "200")
Danilo noticed that from a graph $`G` we can build a category ùë≠$`(G)`, the _free category on the graph_ $`G`. An object is a dot of $`G`, and a map is a path in $`G`. For which of the following graphs does Danilo's category have a terminal object?
:::

:::solution (solutionTo := "Exercise 1")
```savedComment
Exercise 17.1 (p. 200)
```

An object $`S` is terminal in a category if for each object $`X` in the category there is exactly one map from $`X` to $`S`. Only the free category ùë≠$`(G_b)` on the graph (b) and the free category ùë≠$`(G_c)` on the graph (c) have objects that meet this criterion.

\(a\) ùë≠$`(G_a)` has infinitely many maps from the object to itself (each map corresponding to the path formed by going around the arrow/loop a different number of times).

```savedLean -show
namespace Ex17_1_a
```

```savedLean
inductive Dot
  | x‚ÇÅ

inductive Arrow : Dot ‚Üí Dot ‚Üí Type
  | f‚ÇÅ : Arrow .x‚ÇÅ .x‚ÇÅ

instance : Quiver Dot where
  Hom := Arrow

open Limits in
example : ¬¨(HasTerminal (Paths Dot)) := by
  by_contra h
  have h_uniq : Unique (Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÅ) :=
    uniqueToTerminal (C := Paths Dot) Dot.x‚ÇÅ
  have h_sub : Subsingleton (Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÅ) :=
    inferInstance
  have h_nontriv : Nontrivial (Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÅ) := by
    apply nontrivial_iff.mpr
    use Quiver.Path.nil, (Quiver.Hom.toPath Arrow.f‚ÇÅ)
    intro
    contradiction
  exact false_of_nontrivial_of_subsingleton
      (Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÅ)
```

```savedLean -show
end Ex17_1_a
```

\(b\) The only object in ùë≠$`(G_b)` is terminal, since there is exactly one map from that object to itself, namely the identity map.

```savedLean -show
namespace Ex17_1_b
```

```savedLean
inductive Dot
  | x‚ÇÅ

inductive Arrow : Dot ‚Üí Dot ‚Üí Type

instance : Quiver Dot where
  Hom := Arrow

open Limits in
example : HasTerminal (Paths Dot) := by
  have h_all_sub
      : ‚àÄ (x : Paths Dot), Subsingleton (x ‚ü∂ Dot.x‚ÇÅ) := by
    intro x
    apply Subsingleton.intro
    intro f g
    let P : {x : Paths Dot} ‚Üí (x ‚ü∂ Dot.x‚ÇÅ) ‚Üí Prop :=
      fun p => p = Quiver.Path.nil
    have h_all_eq_id : ‚àÄ {x : Paths Dot} (p : x ‚ü∂ Dot.x‚ÇÅ), P p := by
      intros
      apply Paths.induction_fixed_target
      ¬∑ rfl
      ¬∑ intro _ _ _ e _
        nomatch e
    rw [h_all_eq_id f, h_all_eq_id g]
  have h_all_nonempty
      : ‚àÄ (x : Paths Dot), Nonempty (x ‚ü∂ Dot.x‚ÇÅ) := by
    intro x
    exact Nonempty.intro Quiver.Path.nil
  exact hasTerminal_of_unique (C := Paths Dot) Dot.x‚ÇÅ
```

```savedLean -show
end Ex17_1_b
```

\(c\) In ùë≠$`(G_c)`, the second dot from the right corresponds to a terminal object, since there is exactly one map (path) to that object from every object in ùë≠$`(G_c)`. We label the dots from left to right, with $`x_1` being the upper left dot and $`x_2` the lower left dot (so $`x_4` is terminal).

```savedLean -show
namespace Ex17_1_c
```

```savedLean
inductive Dot
  | x‚ÇÅ | x‚ÇÇ | x‚ÇÉ | x‚ÇÑ | x‚ÇÖ

inductive Arrow : Dot ‚Üí Dot ‚Üí Type
  | f‚ÇÅ : Arrow .x‚ÇÅ .x‚ÇÉ
  | f‚ÇÇ : Arrow .x‚ÇÇ .x‚ÇÉ
  | f‚ÇÉ : Arrow .x‚ÇÉ .x‚ÇÑ
  | f‚ÇÖ : Arrow .x‚ÇÖ .x‚ÇÑ

instance : Quiver Dot where
  Hom := Arrow

open Limits in
example : HasTerminal (Paths Dot) := by
  have h_all_sub
      : ‚àÄ (x : Paths Dot), Subsingleton (x ‚ü∂ Dot.x‚ÇÑ) := by
    intro x
    apply Subsingleton.intro
    intro f g
    cases x
    all_goals (
      repeat first
      | (rcases f with _ | ‚ü®f, _, _‚ü©) -- deconstruct f
      | (rcases g with _ | ‚ü®g, _, _‚ü©) -- deconstruct g
      | contradiction
      | rfl
    )
  have h_all_nonempty
      : ‚àÄ (x : Paths Dot), Nonempty (x ‚ü∂ Dot.x‚ÇÑ) := by
    intro x
    cases x
    ¬∑ exact Nonempty.intro
          ((Quiver.Hom.toPath Arrow.f‚ÇÅ).cons Arrow.f‚ÇÉ)
    ¬∑ exact Nonempty.intro
          ((Quiver.Hom.toPath Arrow.f‚ÇÇ).cons Arrow.f‚ÇÉ)
    ¬∑ exact Nonempty.intro
          (Quiver.Hom.toPath Arrow.f‚ÇÉ)
    ¬∑ exact Nonempty.intro
          Quiver.Path.nil
    ¬∑ exact Nonempty.intro
          (Quiver.Hom.toPath Arrow.f‚ÇÖ)
  exact hasTerminal_of_unique (C := Paths Dot) Dot.x‚ÇÑ
```

```savedLean -show
end Ex17_1_c
```

\(d\) ùë≠$`(G_d)` is similar to ùë≠$`(G_a)` in that each object of ùë≠$`(G_d)` has infinitely many maps from itself to itself and from the other object to itself (each map corresponding to the path formed by going a different number of times around the closed loop of arrows between the two objects).

```savedLean -show
namespace Ex17_1_d
```

```savedLean
inductive Dot
  | x‚ÇÅ | x‚ÇÇ

inductive Arrow : Dot ‚Üí Dot ‚Üí Type
  | f‚ÇÅ : Arrow .x‚ÇÅ .x‚ÇÇ
  | f‚ÇÇ : Arrow .x‚ÇÇ .x‚ÇÅ

instance : Quiver Dot where
  Hom := Arrow

open Limits in
example : ¬¨(HasTerminal (Paths Dot)) := by
  by_contra h
  have h_all_uniq
      : ‚àÄ (x : Paths Dot), Unique (x ‚ü∂ ‚ä§_ Paths Dot) := by
    intro x
    exact uniqueToTerminal x
  have h_all_sub
      : ‚àÄ (x : Paths Dot), Subsingleton (x ‚ü∂ ‚ä§_ Paths Dot) := by
    intro x
    infer_instance
  have h_nontriv‚ÇÅ : Nontrivial (Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÅ) := by
    apply nontrivial_iff.mpr
    use Quiver.Path.nil, ((Quiver.Hom.toPath Arrow.f‚ÇÅ).cons Arrow.f‚ÇÇ)
    intro
    contradiction
  have h_nontriv‚ÇÇ : Nontrivial (Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÇ) := by
    apply nontrivial_iff.mpr
    use Quiver.Path.nil, ((Quiver.Hom.toPath Arrow.f‚ÇÇ).cons Arrow.f‚ÇÅ)
    intro
    contradiction
  cases hx : ‚ä§_ (Paths Dot) <;> rw [hx] at h_all_sub
  ¬∑ exact false_of_nontrivial_of_subsingleton
        (Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÅ)
  ¬∑ exact false_of_nontrivial_of_subsingleton
        (Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÇ)
```

```savedLean -show
end Ex17_1_d
```

\(e\) In ùë≠$`(G_e)`, the left-hand object has no map from the right-hand object, while the right-hand object has two maps from the left-hand object. We label the dots from left to right.

```savedLean -show
namespace Ex17_1_e
```

```savedLean
inductive Dot
  | x‚ÇÅ | x‚ÇÇ

inductive Arrow : Dot ‚Üí Dot ‚Üí Type
  | f‚ÇÅ : Arrow .x‚ÇÅ .x‚ÇÇ
  | f‚ÇÇ : Arrow .x‚ÇÅ .x‚ÇÇ

instance : Quiver Dot where
  Hom := Arrow

open Limits in
example : ¬¨(HasTerminal (Paths Dot)) := by
  by_contra h
  have h_all_uniq
      : ‚àÄ (x : Paths Dot), Unique (x ‚ü∂ ‚ä§_ Paths Dot) := by
    intro x
    exact uniqueToTerminal x
  have h_all_sub
      : ‚àÄ (x : Paths Dot), Subsingleton (x ‚ü∂ ‚ä§_ Paths Dot) := by
    intro x
    infer_instance
  have h_empty‚ÇÅ : ¬¨(Nonempty (Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÅ)) := by
    by_contra h_empty
    rcases h_empty with ‚ü®p‚ü©
    nomatch p
  have h_nontriv‚ÇÇ : Nontrivial (Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÇ) := by
    apply nontrivial_iff.mpr
    use (Quiver.Hom.toPath Arrow.f‚ÇÅ), (Quiver.Hom.toPath Arrow.f‚ÇÇ)
    intro H
    injection H with _ h_arrow
    contradiction
  cases hx : ‚ä§_ (Paths Dot)
  ¬∑ rw [hx] at h_all_uniq h_all_sub
    have h_uniq‚ÇÅ : Unique (Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÅ) :=
      h_all_uniq Dot.x‚ÇÇ
    have h_nonempty‚ÇÅ : Nonempty (Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÅ) := by
      infer_instance
    contradiction
  ¬∑ rw [hx] at h_all_sub
    exact false_of_nontrivial_of_subsingleton
        (Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÇ)
```

```savedLean -show
end Ex17_1_e
```

\(f\) In ùë≠$`(G_f)`, neither the leftmost object nor the centre object has a map from the rightmost object, while the rightmost object has no map from either the leftmost object nor the centre object. We label the dots from left to right again.

```savedLean -show
namespace Ex17_1_f
```

```savedLean
inductive Dot
  | x‚ÇÅ | x‚ÇÇ | x‚ÇÉ

inductive Arrow : Dot ‚Üí Dot ‚Üí Type
  | f‚ÇÅ : Arrow .x‚ÇÇ .x‚ÇÅ

instance : Quiver Dot where
  Hom := Arrow

open Limits in
example : ¬¨(HasTerminal (Paths Dot)) := by
  by_contra h
  have h_all_uniq
      : ‚àÄ (x : Paths Dot), Unique (x ‚ü∂ ‚ä§_ Paths Dot) := by
    intro x
    exact uniqueToTerminal x
  have h_all_sub
      : ‚àÄ (x : Paths Dot), Subsingleton (x ‚ü∂ ‚ä§_ Paths Dot) := by
    intro x
    infer_instance
  have h_empty‚ÇÅ : ¬¨(Nonempty (Quiver.Path Dot.x‚ÇÉ Dot.x‚ÇÅ)) := by
    by_contra h_empty
    rcases h_empty with ‚ü®p‚ü©
    match p with
    | .cons p' q =>
      match p' with
      | .nil =>
      nomatch q
  have h_empty‚ÇÇ : ¬¨(Nonempty (Quiver.Path Dot.x‚ÇÉ Dot.x‚ÇÇ)) := by
    by_contra h_empty
    rcases h_empty with ‚ü®p‚ü©
    nomatch p
  have h_empty‚ÇÉ : ¬¨(Nonempty (Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÉ)) := by
    by_contra h_empty
    rcases h_empty with ‚ü®p‚ü©
    nomatch p
  cases hx : ‚ä§_ (Paths Dot) <;> rw [hx] at h_all_uniq h_all_sub
  ¬∑ have h_uniq‚ÇÅ : Unique (Quiver.Path Dot.x‚ÇÉ Dot.x‚ÇÅ) :=
      h_all_uniq Dot.x‚ÇÉ
    have h_nonempty‚ÇÇ : Nonempty (Quiver.Path Dot.x‚ÇÉ Dot.x‚ÇÅ) := by
      infer_instance
    contradiction
  ¬∑ have h_uniq‚ÇÇ : Unique (Quiver.Path Dot.x‚ÇÉ Dot.x‚ÇÇ) :=
      h_all_uniq Dot.x‚ÇÉ
    have h_nonempty‚ÇÇ : Nonempty (Quiver.Path Dot.x‚ÇÉ Dot.x‚ÇÇ) := by
      infer_instance
    contradiction
  ¬∑ have h_uniq‚ÇÉ : Unique (Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÉ) :=
      h_all_uniq Dot.x‚ÇÇ
    have h_nonempty‚ÇÉ : Nonempty (Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÉ) := by
      infer_instance
    contradiction
```

```savedLean -show
end Ex17_1_f
```
:::

# 3. Commuting diagrams

:::definition (definitionTerm := "Commutes") (definitionPage := "201")
We say that a diagram of shape $`G` in ùíû _commutes_ if for each pair $`p`, $`q` of dots in $`G`, all paths in $`G` from $`p` to $`q` are interpreted as the same map in ùíû.
:::

The "interpretation" of dots in $`G` as objects of ùíû and paths in $`G` as maps of ùíû is performed by a functor from the free category ùë≠$`(G)` to ùíû, written in Lean as `CategoryTheory.Paths Dot ‚•§ ùíû`, where `p q : Dot`.

:::question (questionTitle := "Exercise 2") (questionPage := "202")
```savedComment
Exercise 17.2 (p. 202)
```

```savedLean -show
namespace Ex17_2
```

Show that a diagram of shape

```savedLean
inductive Dot
  | x‚ÇÅ | x‚ÇÇ

inductive Arrow : Dot ‚Üí Dot ‚Üí Type
  | f : Arrow .x‚ÇÅ .x‚ÇÇ
  | g : Arrow .x‚ÇÇ .x‚ÇÅ

instance : Quiver Dot where
  Hom := Arrow
```

commutes if and only if the maps assigned to the two arrows are inverse.
:::

:::solution (solutionTo := "Exercise 2")
We need to show that under a functor from the free category ùë≠$`(G)` to ùíû, all paths between each pair of dots in ùë≠$`(G)` correspond to the same map in ùíû if and only if the two arrows in the graph correspond to inverse maps in ùíû. Since there are four possible pairings of dots, the proof of the backward ("if") direction contains four parts.

```savedLean
example {ùíû : Type u} [Category.{v, u} ùíû] (F : Paths Dot ‚•§ ùíû) :

    -- Let x‚ÇÅ' be the object in ùíû associated with Dot.x‚ÇÅ in ùêπ(G)
    let x‚ÇÅ' : ùíû := F.obj Dot.x‚ÇÅ
    -- Let x‚ÇÇ' be the object in ùíû associated with Dot.x‚ÇÇ in ùêπ(G)
    let x‚ÇÇ' : ùíû := F.obj Dot.x‚ÇÇ
    -- Let f' be the morphism in ùíû associated with Arrow.f in ùêπ(G)
    let f' : x‚ÇÅ' ‚ü∂ x‚ÇÇ' := F.map (Quiver.Hom.toPath Arrow.f)
    -- Let g' be the morphism in ùíû associated with Arrow.g in ùêπ(G)
    let g' : x‚ÇÇ' ‚ü∂ x‚ÇÅ' := F.map (Quiver.Hom.toPath Arrow.g)

    -- Dot.x‚ÇÅ ‚ü∂ Dot.x‚ÇÅ is interpreted as the identity on x‚ÇÅ' in ùíû
    (‚àÄ p‚ÇÅ : Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÅ, F.map p‚ÇÅ = ùüô x‚ÇÅ') ‚àß
    -- Dot.x‚ÇÇ ‚ü∂ Dot.x‚ÇÇ is interpreted as the identity on x‚ÇÇ' in ùíû
    (‚àÄ p‚ÇÇ : Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÇ, F.map p‚ÇÇ = ùüô x‚ÇÇ') ‚àß
    -- Dot.x‚ÇÅ ‚ü∂ Dot.x‚ÇÇ is interpreted as f' in ùíû
    (‚àÄ p‚ÇÅ‚ÇÇ : Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÇ, F.map p‚ÇÅ‚ÇÇ = f') ‚àß
    -- Dot.x‚ÇÇ ‚ü∂ Dot.x‚ÇÅ is interpreted as g' in ùíû
    (‚àÄ p‚ÇÇ‚ÇÅ : Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÅ, F.map p‚ÇÇ‚ÇÅ = g') ‚Üî
    -- The maps assigned to the two arrows in G are inverse
    g' ‚äö f' = ùüô x‚ÇÅ' ‚àß f' ‚äö g' = ùüô x‚ÇÇ' := by

  constructor
  -- Proof of the forward ("only if") direction
  ¬∑ repeat rw [‚Üê F.map_comp]
    intro ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÅ‚ÇÇ, h‚ÇÇ‚ÇÅ‚ü©
    constructor
    ¬∑ rw [h‚ÇÅ]
    ¬∑ rw [h‚ÇÇ]
  -- Proof of the backward ("if") direction
  ¬∑ repeat rw [‚Üê F.map_comp]
    intro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    and_intros
    -- ‚àÄ p‚ÇÅ : Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÅ, F.map p‚ÇÅ = ùüô x‚ÇÅ'
    ¬∑ let rec aux‚ÇÅ (p : Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÅ)
          : F.map p = ùüô (F.obj Dot.x‚ÇÅ) := by
        match p with
        | .nil =>
          exact F.map_id Dot.x‚ÇÅ
        | .cons tail Arrow.g =>
          match tail with
          | .cons tail' Arrow.f =>
            change F.map ((Quiver.Hom.toPath Arrow.g ‚äö
                           Quiver.Hom.toPath Arrow.f) ‚äö tail') = _
            rw [Functor.map_comp, h‚ÇÅ, Category.comp_id]
            exact aux‚ÇÅ tail'
      intro p
      exact aux‚ÇÅ p
    -- ‚àÄ p‚ÇÇ : Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÇ, F.map p‚ÇÇ = ùüô x‚ÇÇ'
    ¬∑ let rec aux‚ÇÇ (p : Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÇ)
          : F.map p = ùüô (F.obj Dot.x‚ÇÇ) := by
        match p with
        | .nil =>
          exact F.map_id Dot.x‚ÇÇ
        | .cons tail Arrow.f =>
          match tail with
          | .cons tail' Arrow.g =>
            change F.map ((Quiver.Hom.toPath Arrow.f ‚äö
                           Quiver.Hom.toPath Arrow.g) ‚äö tail') = _
            rw [Functor.map_comp, h‚ÇÇ, Category.comp_id]
            exact aux‚ÇÇ tail'
      intro p
      exact aux‚ÇÇ p
    -- ‚àÄ p‚ÇÅ‚ÇÇ : Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÇ, F.map p‚ÇÅ‚ÇÇ = f'
    ¬∑ let rec aux‚ÇÅ‚ÇÇ (p : Quiver.Path Dot.x‚ÇÅ Dot.x‚ÇÇ)
          : F.map p = F.map (Quiver.Hom.toPath Arrow.f) := by
        match p with
        | .cons tail Arrow.f =>
          match tail with
          | .nil =>
            rfl
          | .cons tail' Arrow.g =>
            change F.map ((Quiver.Hom.toPath Arrow.f ‚äö
                           Quiver.Hom.toPath Arrow.g) ‚äö tail') = _
            rw [Functor.map_comp, h‚ÇÇ, Category.comp_id]
            exact aux‚ÇÅ‚ÇÇ tail'
      intro p
      exact aux‚ÇÅ‚ÇÇ p
    -- ‚àÄ p‚ÇÇ‚ÇÅ : Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÅ, F.map p‚ÇÇ‚ÇÅ = g'
    ¬∑ let rec aux‚ÇÇ‚ÇÅ (p : Quiver.Path Dot.x‚ÇÇ Dot.x‚ÇÅ)
          : F.map p = F.map (Quiver.Hom.toPath Arrow.g) := by
        match p with
        | .cons tail Arrow.g =>
          match tail with
          | .nil =>
            rfl
          | .cons tail' Arrow.f =>
            change F.map ((Quiver.Hom.toPath Arrow.g ‚äö
                           Quiver.Hom.toPath Arrow.f) ‚äö tail') = _
            rw [Functor.map_comp, h‚ÇÅ, Category.comp_id]
            exact aux‚ÇÇ‚ÇÅ tail'
      intro p
      exact aux‚ÇÇ‚ÇÅ p
```

```savedLean -show
end Ex17_2
```
:::

:::question (questionTitle := "Exercise 3") (questionPage := "203")
```savedComment
Exercise 17.3 (p. 203)
```

```savedLean -show
namespace Ex17_3
```

In the diagram

```savedLean
inductive Vertex
  | A | B | C | D | E | F | G | H

inductive Edge : Vertex ‚Üí Vertex ‚Üí Type
  | f : Edge .A .B
  | g : Edge .B .C
  | h : Edge .C .D
  | i : Edge .A .E
  | j : Edge .B .F
  | k : Edge .C .G
  | l : Edge .D .H
  | m : Edge .E .F
  | n : Edge .F .G
  | p : Edge .G .H

instance : Quiver Vertex where
  Hom := Edge
```

the three equations (1) $`{jf = mi}`, (2) $`{kg = nj}`, (3) $`{lh = pk}` actually force the diagram to commute; but you are just asked to prove that
$$`pnmi = lhgf`
:::

:::solution (solutionTo := "Exercise 3")
We can prove this directly in the free category on the graph using only the associative property of path composition.

```savedLean
open Edge Quiver Quiver.Hom Quiver.Path Vertex in
example
    (h‚ÇÅ : (toPath f |>.comp (toPath j)) =
          (toPath i |>.comp (toPath m) : Path A F))
    (h‚ÇÇ : (toPath g |>.comp (toPath k)) =
          (toPath j |>.comp (toPath n) : Path B G))
    (h‚ÇÉ : (toPath h |>.comp (toPath l)) =
          (toPath k |>.comp (toPath p) : Path C H))
    : (toPath i
          |>.comp (toPath m)
          |>.comp (toPath n)
          |>.comp (toPath p)) =
      (toPath f
          |>.comp (toPath g)
          |>.comp (toPath h)
          |>.comp (Hom.toPath l) : Path A H) := by
  rw [comp_assoc, ‚Üê h‚ÇÅ]
  rw [‚Üê comp_assoc, comp_assoc (a := A) (toPath f), ‚Üê h‚ÇÇ]
  rw [comp_assoc, comp_assoc, ‚Üê h‚ÇÉ]
  repeat rw [‚Üê comp_assoc]
```

```savedLean -show
end Ex17_3
```
:::

:::question (questionTitle := "Exercise 4") (questionPage := "203")
For each of these diagrams, find a shortest list of equations that will make it commute.

...

After you have found the answers try to explain clearly how you know, from the equations you chose, that _all_ possible paths give equal composites.
:::

:::solution (solutionTo := "Exercise 4")
```savedComment
Exercise 17.4 (p. 203)
```

\(a\) A shortest list of equations that will make diagram (a) commute is:
$$`gf = 1_A, \quad fg = 1_B, \quad g = h`

(cf. Exercise 2 above.)

```savedLean -show
namespace Ex17_4_a
```

```savedLean
inductive Vertex
  | A | B

inductive Edge : Vertex ‚Üí Vertex ‚Üí Type
  | f : Edge .A .B
  | g : Edge .B .A
  | h : Edge .B .A

instance : Quiver Vertex where
  Hom := Edge

example {ùíû : Type u} [Category.{v, u} ùíû] (F : Paths Vertex ‚•§ ùíû) :

    -- Let A' be the object in ùíû associated with Vertex.A in ùêπ(G)
    let A' : ùíû := F.obj Vertex.A
    -- Let B' be the object in ùíû associated with Vertex.B in ùêπ(G)
    let B' : ùíû := F.obj Vertex.B
    -- Let f' be the morphism in ùíû associated with Edge.f in ùêπ(G)
    let f' : A' ‚ü∂ B' := F.map (Quiver.Hom.toPath Edge.f)
    -- Let g' be the morphism in ùíû associated with Edge.g in ùêπ(G)
    let g' : B' ‚ü∂ A' := F.map (Quiver.Hom.toPath Edge.g)
    -- Let h' be the morphism in ùíû associated with Edge.h in ùêπ(G)
    let h' : B' ‚ü∂ A' := F.map (Quiver.Hom.toPath Edge.h)

    -- The 3 equations required to make the diagram commute
    g' ‚äö f' = ùüô A' ‚àß
    f' ‚äö g' = ùüô B' ‚àß
    g' = h' ‚Üí
    -- .A ‚ü∂ .A is interpreted as the identity on A' in ùíû
    (‚àÄ p : Quiver.Path Vertex.A Vertex.A, F.map p = ùüô A') ‚àß
    -- .B ‚ü∂ .B is interpreted as the identity on B' in ùíû
    (‚àÄ p : Quiver.Path Vertex.B Vertex.B, F.map p = ùüô B') ‚àß
    -- .A ‚ü∂ .B is interpreted as f' in ùíû
    (‚àÄ p : Quiver.Path Vertex.A Vertex.B, F.map p = f') ‚àß
    -- .B ‚ü∂ .A is interpreted as g' in ùíû
    (‚àÄ p : Quiver.Path Vertex.B Vertex.A, F.map p = g') := by

  intro A' B' f' g' h' ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©
  dsimp [A', B', f', g', h'] at h‚ÇÅ h‚ÇÇ h‚ÇÉ
  rw [‚Üê F.map_comp] at h‚ÇÅ h‚ÇÇ
  and_intros
  -- ‚àÄ p : Quiver.Path Vertex.A Vertex.A, F.map p = ùüô A'
  ¬∑ let rec auxAA (p : Quiver.Path Vertex.A Vertex.A)
        : F.map p = ùüô (F.obj Vertex.A) := by
      match p with
      | .nil =>
        exact F.map_id _
      | .cons (.cons tail Edge.f) (e : Vertex.B ‚ü∂ Vertex.A) =>
        have : F.map ((tail.cons Edge.f).cons e) =
               F.map ((Quiver.Hom.toPath Edge.g ‚äö
                       Quiver.Hom.toPath Edge.f) ‚äö tail) := by
          rcases e
          ¬∑ rfl
          ¬∑ change F.map (Quiver.Hom.toPath Edge.h ‚äö
                          Quiver.Hom.toPath Edge.f ‚äö tail) = _
            rw [Functor.map_comp, ‚Üê h‚ÇÉ, ‚Üê Functor.map_comp,
                Category.assoc]
        rw [this]
        rw [Functor.map_comp, h‚ÇÅ, Category.comp_id]
        exact auxAA tail
    intro p
    exact auxAA p
  -- ‚àÄ p : Quiver.Path Vertex.B Vertex.B, F.map p = ùüô B'
  ¬∑ let rec auxBB (p : Quiver.Path Vertex.B Vertex.B)
        : F.map p = ùüô (F.obj Vertex.B) := by
      match p with
      | .nil =>
        exact F.map_id _
      | .cons (.cons tail (e : Vertex.B ‚ü∂ Vertex.A)) Edge.f =>
        have : F.map ((tail.cons e).cons Edge.f) =
               F.map ((Quiver.Hom.toPath Edge.f ‚äö
                       Quiver.Hom.toPath Edge.g) ‚äö tail) := by
          rcases e
          ¬∑ rfl
          ¬∑ change F.map (Quiver.Hom.toPath Edge.f ‚äö
                          Quiver.Hom.toPath Edge.h ‚äö tail) = _
            rw [Functor.map_comp, Functor.map_comp, ‚Üê h‚ÇÉ,
                ‚Üê Functor.map_comp, ‚Üê Functor.map_comp,
                Category.assoc]
        rw [this]
        rw [Functor.map_comp, h‚ÇÇ, Category.comp_id]
        exact auxBB tail
    intro p
    exact auxBB p
  -- ‚àÄ p : Quiver.Path Vertex.A Vertex.B, F.map p = f'
  ¬∑ let rec auxAB (p : Quiver.Path Vertex.A Vertex.B)
        : F.map p = F.map (Quiver.Hom.toPath Edge.f) := by
      match p with
      | .cons .nil Edge.f =>
        rfl
      | .cons (.cons tail (e : Vertex.B ‚ü∂ Vertex.A)) Edge.f =>
        have : F.map ((tail.cons e).cons Edge.f) =
               F.map ((Quiver.Hom.toPath Edge.f ‚äö
                       Quiver.Hom.toPath Edge.g) ‚äö tail) := by
          rcases e
          ¬∑ rfl
          ¬∑ change F.map ((Quiver.Hom.toPath Edge.f ‚äö
                           Quiver.Hom.toPath Edge.h) ‚äö tail) = _
            rw [Functor.map_comp, Functor.map_comp, ‚Üê h‚ÇÉ,
                ‚Üê Functor.map_comp, ‚Üê Functor.map_comp]
        rw [this]
        rw [Functor.map_comp, h‚ÇÇ, Category.comp_id]
        exact auxAB tail
    intro p
    exact auxAB p
  -- ‚àÄ p : Quiver.Path Vertex.B Vertex.A, F.map p = g'
  ¬∑ let rec auxBA (p : Quiver.Path Vertex.B Vertex.A)
        : F.map p = F.map (Quiver.Hom.toPath Edge.g) := by
      match p with
      | .cons .nil (e : Vertex.B ‚ü∂ Vertex.A) =>
        rcases e
        ¬∑ rfl
        ¬∑ rw [h‚ÇÉ]
          rfl
      | .cons (.cons tail Edge.f) (e : Vertex.B ‚ü∂ Vertex.A) =>
        have : F.map ((tail.cons Edge.f).cons e) =
               F.map ((Quiver.Hom.toPath Edge.g ‚äö
                       Quiver.Hom.toPath Edge.f) ‚äö tail) := by
          rcases e
          ¬∑ rfl
          ¬∑ change F.map ((Quiver.Hom.toPath Edge.h ‚äö
                           Quiver.Hom.toPath Edge.f) ‚äö tail) = _
            rw [Functor.map_comp, Functor.map_comp, ‚Üê h‚ÇÉ,
                ‚Üê Functor.map_comp, ‚Üê Functor.map_comp]
        rw [this]
        rw [Functor.map_comp, h‚ÇÅ, Category.comp_id]
        exact auxBA tail
    intro p
    exact auxBA p
```

```savedLean -show
end Ex17_4_a
```

\(b\) A shortest list of equations that will make diagram (b) commute is:
$$`hgf = 1_A, \quad fhg = 1_B, \quad gfh = 1_C`

Since there are now nine possible pairings of dots, we change our approach from Exercises 2 and 4(a) above and instead employ a more efficient proof by induction.

```savedLean -show
namespace Ex17_4_b
```

```savedLean
inductive Vertex
  | A | B | C

inductive Edge : Vertex ‚Üí Vertex ‚Üí Type
  | f : Edge .A .B
  | g : Edge .B .C
  | h : Edge .C .A

instance : Quiver Vertex where
  Hom := Edge

example {ùíû : Type u} [Category.{v, u} ùíû] (F : Paths Vertex ‚•§ ùíû) :

    -- Let A' be the object in ùíû associated with Vertex.A in ùêπ(G)
    let A' : ùíû := F.obj Vertex.A
    -- Let B' be the object in ùíû associated with Vertex.B in ùêπ(G)
    let B' : ùíû := F.obj Vertex.B
    -- Let C' be the object in ùíû associated with Vertex.C in ùêπ(G)
    let C' : ùíû := F.obj Vertex.C
    -- Let f' be the morphism in ùíû associated with Edge.f in ùêπ(G)
    let f' : A' ‚ü∂ B' := F.map (Quiver.Hom.toPath Edge.f)
    -- Let g' be the morphism in ùíû associated with Edge.g in ùêπ(G)
    let g' : B' ‚ü∂ C' := F.map (Quiver.Hom.toPath Edge.g)
    -- Let h' be the morphism in ùíû associated with Edge.h in ùêπ(G)
    let h' : C' ‚ü∂ A' := F.map (Quiver.Hom.toPath Edge.h)

    -- The 3 equations required to make the diagram commute
    h' ‚äö g' ‚äö f' = ùüô A' ‚àß
    f' ‚äö h' ‚äö g' = ùüô B' ‚àß
    g' ‚äö f' ‚äö h' = ùüô C' ‚Üí
    -- .A ‚ü∂ .A is interpreted as the identity on A' in ùíû
    (‚àÄ p : Quiver.Path Vertex.A Vertex.A, F.map p = ùüô A') ‚àß
    -- .B ‚ü∂ .B is interpreted as the identity on B' in ùíû
    (‚àÄ p : Quiver.Path Vertex.B Vertex.B, F.map p = ùüô B') ‚àß
    -- .C ‚ü∂ .C is interpreted as the identity on C' in ùíû
    (‚àÄ p : Quiver.Path Vertex.C Vertex.C, F.map p = ùüô C') ‚àß
    -- .A ‚ü∂ .B is interpreted as f' in ùíû
    (‚àÄ p : Quiver.Path Vertex.A Vertex.B, F.map p = f') ‚àß
    -- .B ‚ü∂ .C is interpreted as g' in ùíû
    (‚àÄ p : Quiver.Path Vertex.B Vertex.C, F.map p = g') ‚àß
    -- .C ‚ü∂ .A is interpreted as h' in ùíû
    (‚àÄ p : Quiver.Path Vertex.C Vertex.A, F.map p = h') ‚àß
    -- .A ‚ü∂ .C is interpreted as g' ‚äö f' in ùíû
    (‚àÄ p : Quiver.Path Vertex.A Vertex.C, F.map p = g' ‚äö f') ‚àß
    -- .B ‚ü∂ .A is interpreted as h' ‚äö g' in ùíû
    (‚àÄ p : Quiver.Path Vertex.B Vertex.A, F.map p = h' ‚äö g') ‚àß
    -- .C ‚ü∂ .B is interpreted as f' ‚äö h' in ùíû
    (‚àÄ p : Quiver.Path Vertex.C Vertex.B, F.map p = f' ‚äö h') := by

  intro A' B' C' f' g' h' ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©
  suffices ‚àÄ {u v : Vertex} (p : Quiver.Path u v), F.map p =
    match u, v with
    | .A, .A => ùüô A'
    | .B, .B => ùüô B'
    | .C, .C => ùüô C'
    | .A, .B => f'
    | .B, .C => g'
    | .C, .A => h'
    | .A, .C => g' ‚äö f'
    | .B, .A => h' ‚äö g'
    | .C, .B => f' ‚äö h' by
    simp [this]
  dsimp [A', B', C', f', g', h'] at h‚ÇÅ h‚ÇÇ h‚ÇÉ
  intro u v p
  induction p with
  | nil =>
    cases u
    all_goals
      dsimp
      exact F.map_id _
  | cons p e ih =>
    change F.map (Quiver.Hom.toPath e ‚äö p) = _
    rw [F.map_comp, ih]
    cases u <;> rcases e
    all_goals
      dsimp [A', B', C', f', g', h']
      try rw [Category.id_comp]
      try first | rw [h‚ÇÅ] | rw [h‚ÇÇ] | rw [h‚ÇÉ]
```

```savedLean -show
end Ex17_4_b
```

\(c\) A shortest list of equations that will make diagram (c) commute is:
$$`jhg = 1_A, \quad gjh = 1_B, \quad hgj = 1_C, \quad f = g, \quad i = 1_C`

```savedLean -show
namespace Ex17_4_c
```

```savedLean
inductive Vertex
  | A | B | C

inductive Edge : Vertex ‚Üí Vertex ‚Üí Type
  | f : Edge .A .B
  | g : Edge .A .B
  | h : Edge .B .C
  | i : Edge .C .C
  | j : Edge .C .A

instance : Quiver Vertex where
  Hom := Edge

example {ùíû : Type u} [Category.{v, u} ùíû] (F : Paths Vertex ‚•§ ùíû) :

    -- Let A' be the object in ùíû associated with Vertex.A in ùêπ(G)
    let A' : ùíû := F.obj Vertex.A
    -- Let B' be the object in ùíû associated with Vertex.B in ùêπ(G)
    let B' : ùíû := F.obj Vertex.B
    -- Let C' be the object in ùíû associated with Vertex.C in ùêπ(G)
    let C' : ùíû := F.obj Vertex.C
    -- Let f' be the morphism in ùíû associated with Edge.f in ùêπ(G)
    let f' : A' ‚ü∂ B' := F.map (Quiver.Hom.toPath Edge.f)
    -- Let g' be the morphism in ùíû associated with Edge.g in ùêπ(G)
    let g' : A' ‚ü∂ B' := F.map (Quiver.Hom.toPath Edge.g)
    -- Let h' be the morphism in ùíû associated with Edge.h in ùêπ(G)
    let h' : B' ‚ü∂ C' := F.map (Quiver.Hom.toPath Edge.h)
    -- Let i' be the morphism in ùíû associated with Edge.i in ùêπ(G)
    let i' : C' ‚ü∂ C' := F.map (Quiver.Hom.toPath Edge.i)
    -- Let j' be the morphism in ùíû associated with Edge.j in ùêπ(G)
    let j' : C' ‚ü∂ A' := F.map (Quiver.Hom.toPath Edge.j)

    -- The 5 equations required to make the diagram commute
    j' ‚äö h' ‚äö g' = ùüô A' ‚àß
    g' ‚äö j' ‚äö h' = ùüô B' ‚àß
    h' ‚äö g' ‚äö j' = ùüô C' ‚àß
    f' = g' ‚àß
    i' = ùüô C' ‚Üí
    -- .A ‚ü∂ .A is interpreted as the identity on A' in ùíû
    (‚àÄ p : Quiver.Path Vertex.A Vertex.A, F.map p = ùüô A') ‚àß
    -- .B ‚ü∂ .B is interpreted as the identity on B' in ùíû
    (‚àÄ p : Quiver.Path Vertex.B Vertex.B, F.map p = ùüô B') ‚àß
    -- .C ‚ü∂ .C is interpreted as the identity on C' in ùíû
    (‚àÄ p : Quiver.Path Vertex.C Vertex.C, F.map p = ùüô C') ‚àß
    -- .A ‚ü∂ .B is interpreted as g' in ùíû
    (‚àÄ p : Quiver.Path Vertex.A Vertex.B, F.map p = g') ‚àß
    -- .B ‚ü∂ .C is interpreted as h' in ùíû
    (‚àÄ p : Quiver.Path Vertex.B Vertex.C, F.map p = h') ‚àß
    -- .C ‚ü∂ .A is interpreted as j' in ùíû
    (‚àÄ p : Quiver.Path Vertex.C Vertex.A, F.map p = j') ‚àß
    -- .A ‚ü∂ .C is interpreted as h' ‚äö g' in ùíû
    (‚àÄ p : Quiver.Path Vertex.A Vertex.C, F.map p = h' ‚äö g') ‚àß
    -- .B ‚ü∂ .A is interpreted as j' ‚äö h' in ùíû
    (‚àÄ p : Quiver.Path Vertex.B Vertex.A, F.map p = j' ‚äö h') ‚àß
    -- .C ‚ü∂ .B is interpreted as g' ‚äö j' in ùíû
    (‚àÄ p : Quiver.Path Vertex.C Vertex.B, F.map p = g' ‚äö j') := by

  intro A' B' C' f' g' h' i' j' ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, h‚ÇÑ, h‚ÇÖ‚ü©
  suffices ‚àÄ {u v : Vertex} (p : Quiver.Path u v), F.map p =
    match u, v with
    | .A, .A => ùüô A'
    | .B, .B => ùüô B'
    | .C, .C => ùüô C'
    | .A, .B => g'
    | .B, .C => h'
    | .C, .A => j'
    | .A, .C => h' ‚äö g'
    | .B, .A => j' ‚äö h'
    | .C, .B => g' ‚äö j' by
    simp [this]
  dsimp [A', B', C', f', g', h', i', j'] at h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ h‚ÇÖ
  intro u v p
  induction p with
  | nil =>
    cases u
    all_goals
      dsimp
      exact F.map_id _
  | cons p e ih =>
    change F.map (Quiver.Hom.toPath e ‚äö p) = _
    rw [F.map_comp, ih]
    cases u <;> rcases e
    all_goals
      dsimp [A', B', C', f', g', h', i', j']
      try rw [h‚ÇÑ]
      try rw [h‚ÇÖ]
      try first | rw [Category.comp_id] | rw [Category.id_comp]
      try first | rw [h‚ÇÅ] | rw [h‚ÇÇ] | rw [h‚ÇÉ]
```

```savedLean -show
end Ex17_4_c
```
:::

```savedLean -show
end CM
```
